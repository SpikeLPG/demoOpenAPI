plugins {
    id 'java'
    id 'org.springframework.boot' version '3.0.2'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
    id "io.freefair.lombok" version "6.6.1"
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    asciidoctorExtensions
}

repositories {
    mavenCentral()
}

// Generate the RestDoc html before packaging
bootJar {
    archiveBaseName = 'application'
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

test {
    useJUnitPlatform()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // Dependencies required by OpenAPI
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.1.0'

    // Dependencies required by RestDoc & MockMVC
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    asciidoctorExtensions 'org.springframework.restdocs:spring-restdocs-asciidoctor'
}

asciidoctor {
    configurations "asciidoctorExtensions"
    sourceDir  file('src/docs')
    outputDir file('src/docs/html5')
    inputs.dir file("build/generated-snippets")
    dependsOn test
}

bootRun {
    jvmArgs '-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=*:5005'
}
